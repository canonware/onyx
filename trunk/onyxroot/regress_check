#!/usr/bin/env onyx
#
# regress_check <prefix> <srcdir> [<conf>...]

#$no_act true def
$no_act false def

argv length 3 lt {
    stderr `regress_check usage: regress_check <prefix> <srcdir> [<conf>...]\n'
    write pop
    1 die
} if

$PREFIX argv 1 get def

$SRCDIR argv 2 get def
$OBJDIR PREFIX `/obj' cat def
$DSTDIR PREFIX `/dst' cat def

$conf [
    argv length 3 gt {
	argv 3 argv length 3 sub getinterval {} foreach
    } if
    `--prefix=' PREFIX `/dst' 3 ncat
    `-q'
] def

$COOK `cook' path def
$COOKOPT [`-nte'] def
#$COOKPAR [`-par' `8'] def
$COOKPAR [] def

$libs [`libonyx'] def
$bins [`onyx'] def

#
# Procedures.
#

# #obj log_print -
$log_print {
    `==[regress_check (' PREFIX `)]==> ' 4 ndn 1 sprints `\n' 5 ncat
    print flush
} def

# #obj log_eprint -
$log_eprint {
    `==[regress_check (' PREFIX `)]==>> ' 4 ndn 1 sprints `\n' 5 ncat
    print flush
} def

# #arr call -
$call {
    dup log_print
    no_act false eq {
	dup system 0 ne {
	    log_eprint
	    1 die
	}{
	    # Success.
	    pop
	} ifelse
    }{
	pop
    } ifelse
} def

# #target cook -
$cook {
    [
	exch
	COOK exch
	COOKOPT {exch} foreach
    ] call
} def

# #target cookpar -
$cookpar {
    [
	exch
	COOK exch
	COOKOPT {exch} foreach
	COOKPAR {exch} foreach
    ] call
} def

# Recursively delete directories, files, and symlinks in a directory tree.
#
# #dirname deltree -
$deltree {
    no_act false eq {
	dup `e' test {
	    dup {
		over `/' dn 3 ncat
		dup `f' test {
		    unlink
		}{ dup `L' test {
		    unlink
		}{ dup `d' test {
		    deltree
		}{
		    `Unknown file type for: ' exch cat log_eprint
		    1 die
		} ifelse
		} ifelse
		} ifelse
	    } dirforeach
	    rmdir
	} if
    }{
	pop
    } ifelse
} def

# Make sure a directory tree contains nothing but directories.
#
# #dir dir_empty -
$tree_empty {
    no_act false eq {
	dup {
	    over `/' dn 3 ncat
	    dup `d' test {
		tree_empty
	    }{
		`Directory tree not empty' log_eprint
		1 die
	    } ifelse
	} dirforeach
    } if
    pop
} def

#
# Set up.
#

`Start' log_print

PREFIX ` deltree' cat log_print
PREFIX deltree

PREFIX ` mkdir' cat log_print
no_act false eq {
    PREFIX mkdir
} if

OBJDIR ` mkdir' cat log_print
no_act false eq {
    OBJDIR mkdir
} if

OBJDIR ` cd' cat log_print
no_act false eq {
    OBJDIR cd
} if

[
    `../../' SRCDIR `/configure' 3 ncat
    conf {} foreach
] call

#
# Run tests.
#

# Libraries
libs {
    # Parallelizable.
    dup cookpar

    # Non-parallelizable.
    aup
    [
	bdup `_libs_install' cat
	bdup `_libs_uninstall' cat
	`clean'
    ]{
	cook
    } foreach
    bpop

    # Make sure things were adequately cleaned up.
    DSTDIR tree_empty
} foreach

# Binaries.
bins {
    # Parallelizable.
    dup cookpar

    # Non-parallelizable.
    aup
    [
	bdup `_bins_install' cat
    ]{
	cook
    } foreach

    # Make sure the application runs.
    [
	DSTDIR `/bin/' bdup 3 ncat
	`-h'
    ] call
    bpop

    # Non-parallelizable.
    [
	`uninstall'
	`clean'
    ]{
	cook
    } foreach

    # Make sure things were adequately cleaned up.
    DSTDIR tree_empty
} foreach

# General targets.
# Parallelizable.
[
    `bins'
    `mods'
    `libs'
    `check'
]{
    cookpar
} foreach

# Non-parallelizable.
[
    `install' `install' # Yes, this is intentional.
    `uninstall'
    `distclean'
]{
    cook
} foreach

# Make sure things were adequately cleaned up.
DSTDIR tree_empty
OBJDIR tree_empty

`Finish' log_print
