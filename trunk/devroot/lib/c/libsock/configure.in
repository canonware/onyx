dnl
dnl <Copyright = jasone>
dnl <License>
dnl
dnl Version: <Version>
dnl
dnl Process this file with autoconf to produce a configure script.
AC_INIT(include/libsock/libsock.h)
AC_CONFIG_HEADER(include/libsock/libsock_defs.h)

dnl Check for programs.
dnl Trick AC_PROG_CC into not defining CFLAGS, even if it wasn't specified on
dnl the command line.
CFLAGS=$CFLAGS
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PATH_PROG(AR, ar, , $PATH)
AC_PATH_PROG(PERL, perl, , $PATH)
AC_PATH_PROG(CHMOD, chmod, , $PATH)
AC_PATH_PROG(BASENAME, basename, , $PATH)
AC_PATH_PROG(DIFF, diff, , $PATH)
AC_PATH_PROG(CAT, cat, , $PATH)
AC_PATH_PROG(ETAGS, etags, , $PATH)

SQRL_DISABLE_SHARED()

AC_ARG_WITH(libstash-prefix, [  --with-libstash-prefix  Path to libstash])
if test ! -z "$with_libstash_prefix"; then
  CPPFLAGS="$CPPFLAGS -I$with_libstash_prefix/include"
  LDFLAGS="$LDFLAGS -L$with_libstash_prefix/lib"
else
  if test -d "../libstash"; then
    CPPFLAGS="$CPPFLAGS -I../libstash/include"
    LDFLAGS="$LDFLAGS -L../libstash/lib"
  else
    if test "x$prefix" != "xNONE"; then
      CPPFLAGS="$CPPFLAGS -I$prefix/include"
      LDFLAGS="$LDFLAGS -L$prefix/lib"
    fi
  fi
fi

SQRL_USE_PTHREADS()

AC_ARG_WITH(debug, [  --with-debug            Define debugging macros and link binaries with
                          debugging libraries])

dnl Check for libraries.
SQRL_USE_LIBSTASH_R(_d, TEST_LIBS, ..)
AC_SUBST(TEST_LIBS)

if test "x$with_debug" = "xyes"; then
  SQRL_USE_LIBSTASH_R(_d, BIN_LIBS, ..)
  BIN_LIBSOCK="lib/libsock_d.a"
  BIN_DEFINES="\$(TEST_DEFINES)"
  BIN_CFLAGS="\$(TEST_CFLAGS)"
else
  SQRL_USE_LIBSTASH_R(, BIN_LIBS, ..)
  BIN_LIBSOCK="lib/libsock.a"
  BIN_DEFINES=""
  BIN_CFLAGS="\$(ST_CFLAGS) \$(O_CFLAGS)"
fi
AC_SUBST(BIN_LIBS)
AC_SUBST(BIN_LIBSOCK)
AC_SUBST(BIN_DEFINES)
AC_SUBST(BIN_CFLAGS)

dnl Extra libraries for particular platforms.
case "${host}" in
  i386-*-freebsd*)
        ;;
  *-*-linux*)
        ;;
  *-*-solaris2*)
	LIBS="$LIBS -lposix4 -lsocket -lnsl"
        ;;
esac

dnl Check for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h, , AC_MSG_ERROR(Cannot build without unistd.h))

dnl Check for typedefs, structures, and compiler characteristics.
dnl AC_C_BIGENDIAN

AC_MSG_RESULT(================================================================)
AC_MSG_RESULT(Extra libraries to link in: ${LIBS})
AC_MSG_RESULT(Extra libraries for binaries: ${BIN_LIBS})
AC_MSG_RESULT(Version of libsock to link bins with: ${BIN_LIBSOCK})
AC_MSG_RESULT(Extra libraries for tests: ${TEST_LIBS})
AC_MSG_RESULT(Extra ld flags: ${LDFLAGS})
AC_MSG_RESULT(Extra Defines: ${DEFS})
AC_MSG_RESULT(Compiler flags: ${CFLAGS})
AC_MSG_RESULT(C preprocessor flags: ${CPPFLAGS})
AC_MSG_RESULT(================================================================)

SQRL_SET_MAKEFILE

dnl Process the following files.
AC_OUTPUT(Makefile.bsd \
Makefile.gnu
test/pmerge \
test/pstub)
