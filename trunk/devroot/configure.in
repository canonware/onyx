dnl 
dnl <Copyright = jasone>
dnl <License>
dnl 
dnl Version: <Version>
dnl 
dnl Process this file with autoconf to produce a configure script.
AC_INIT(Cookfile.in)

dnl 
dnl Version requirements for external software we depend on.
dnl 
GTK_VERSION_REQUIRED=1.3.12

abs_srcdir=`cd $srcdir; pwd`
AC_SUBST(abs_srcdir)

objdir=.
AC_SUBST(objdir)
abs_objdir=`pwd`
AC_SUBST(abs_objdir)

cfgoutputs="Cookfile bin/Cookfile.inc doc/Cookfile.inc examples/Cookfile.inc"
cfgoutputs="$cfgoutputs lib/Cookfile.inc mod/Cookfile.inc test/Cookfile.inc"
cfgoutputs="$cfgoutputs test/verify"
cfghdrs=
bins=
mods=
libs=
docs=

dnl If CFLAGS isn't defined and using gcc, set CFLAGS to something reasonable.
dnl Otherwise, just prevent autoconf from molesting CFLAGS.
CFLAGS=$CFLAGS
AC_PROG_CC
if test "x$CFLAGS" = "x" ; then
  no_CFLAGS="yes"
fi
if test "x$no_CFLAGS" = "xyes" -a "x$GCC" = "xyes" ; then
	CFLAGS="-Wall -Wno-uninitialized -pipe -g3"
fi
AC_PROG_CPP

dnl Use threads by default.
CW_ENABLE_THREADS

dnl Enable ral support by default.
CW_ENABLE_REAL

dnl Support POSIX file operations by default.
CW_ENABLE_POSIX_FILE

dnl Support POSIX by default.
CW_ENABLE_POSIX

dnl Enable inline functions by default.
CW_ENABLE_INLINES

dnl Build libonyx by default.
CW_ENABLE_LIBONYX

dnl Use libedit by default.
CW_ENABLE_LIBEDIT

dnl Build onyx by default.
CW_ENABLE_ONYX

dnl Build slate by default.
if test "x$enable_threads" = "x1" -a "x$enable_real" = "x1" \
     -a "x$enable_posix" = "x1" -a "x$enable_posix_file" = "x1" ; then
  CW_ENABLE_SLATE
else
  AC_MSG_RESULT(Missing configuration options necessary for slate)
fi

dnl Do not compile with debugging by default.
CW_DISABLE_DEBUG

dnl Do not compile with profiling by default.
CW_DISABLE_PROFILE

dnl Only optimize if not debugging.
if test "x$enable_debug" = "x0" -a "x$no_CFLAGS" = "xyes" ; then
  CFLAGS="$CFLAGS -O"
fi

if test "x$enable_profile" = "x1" -a "x$no_CFLAGS" = "xyes" ; then
  CFLAGS="$CFLAGS -pg"
fi

dnl Look for libraries.  Order matters.  They should appear in an order that
dnl works on the link line (-lonyx -ledit).
if test "x$enable_onyx" = "x1" ; then
  CW_BUILD_LIB(libonyx, lib_onyx)
fi
if test "x$enable_libedit" = "x1" ; then
  CW_BUILD_LIB(libedit, lib_edit)
fi

dnl Look for modules.
enable_modules="yes"
AC_CHECK_FUNCS(dlopen, AC_DEFINE(HAVE_DLOPEN), \
  AC_CHECK_LIB(dl, dlopen, LIBS="$LIBS -ldl" ; AC_DEFINE(HAVE_DLOPEN), \
    [enable_modules="no"
    AC_MSG_RESULT(Modules disabled since dlopen is missing)]))

if test "x$enable_modules" = "xyes" -a "x$enable_threads" = "x1" \
     -a "x$enable_real" = "x1" -a "x$enable_posix" = "x1" \
     -a "x$enable_posix_file" = "x1" ; then
  dnl modgtk.
  PKG_CHECK_MODULES(GTK, gtk+-2.0 >= $GTK_VERSION_REQUIRED, \
    CW_BUILD_MOD(modgtk, mod_gtk), \
    AC_MSG_RESULT(gtk+ $GTK_VERSION_REQUIRED or newer not found))
else
  AC_MSG_RESULT(Missing configuration options necessary for modgtk)
fi

dnl Look for binaries.
if test "x$enable_onyx" = "x1" ; then
  CW_BUILD_BIN(onyx, bin_onyx)
fi
if test "x$enable_slate" = "x1" ; then
  CW_BUILD_BIN(slate, bin_slate)
fi

AC_CONFIG_HEADER($cfghdrs)
AC_SUBST(cfghdrs)
AC_SUBST(cfgoutputs)
AC_SUBST(bins)
AC_SUBST(mods)
AC_SUBST(libs)
AC_SUBST(docs)

AC_CANONICAL_HOST
case "${host}" in
  *-*-darwin*)
	AC_DEFINE(CW_OS_DARWIN, )
	abi="macho"
	CFLAGS="$CFLAGS -fno-common -no-cpp-precomp"
	;;
  *-*-freebsd*)
	AC_DEFINE(CW_OS_FREEBSD, )
	abi="elf"
	RPATH="-Wl,-rpath,"
        ;;
  *-*-linux*)
	AC_DEFINE(CW_OS_LINUX, )
	abi="elf"
	RPATH="-Wl,-rpath,"
        ;;
  *-*-solaris2*)
	AC_DEFINE(CW_OS_SOLARIS, )
	AC_DEFINE(_POSIX_PTHREAD_SEMANTICS)
	abi="elf"
	RPATH="-Wl,-R,"
	LIBS="$LIBS -lposix4"
        ;;
  *)
	AC_MSG_RESULT(Unsupported operating system: ${host})
	RPATH="-Wl,-rpath,"
	;;
esac
AC_SUBST(abi)
AC_SUBST(RPATH)

AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PATH_PROG(AR, ar, , $PATH)
AC_PATH_PROG(LD, ld, , $PATH)
AC_PATH_PROG(PERL, perl, , $PATH)

dnl Do not build documentaion by default; instead use the prebuilt
dnl documentation.
CW_WITHOUT_DOCS()
dnl Include rules for building/installing documents, if the docs are included.
dnl These tests are orthogonal to the tests in CW_WITHOUT_DOCS.
CW_BUILD_DOC(onyx, doc_onyx)
CW_BUILD_DOC(slate, doc_slate)

VERIFY="$objdir/test/verify"
mkdir -p $objdir/test
AC_SUBST(VERIFY)

dnl Find libraries.

dnl Define preprocessor macros.

dnl Check for header files.
if test "x$enable_posix_file" = "x1" ; then
  dnl Make sure that poll() and/or select() is available.
  can_poll=0
  AC_CHECK_FUNCS(poll select, can_poll=1)
  if test "x$can_poll" = "x0" ; then
    AC_MSG_ERROR(Cannot build without poll() or select())
  fi

  AC_CHECK_FUNCS(readdir_r)
fi
if test "x$enable_posix" = "x1" ; then
  AC_CHECK_HEADERS(unistd.h, , AC_MSG_ERROR(Cannot build without unistd.h))
fi

dnl Check for typedefs, structures, and compiler characteristics.
AC_C_BIGENDIAN

dnl 
dnl libonyx configuration.
dnl 
if test "x$lib_onyx" = "x1" ; then
  AC_CHECK_SIZEOF(signed char, 1)
  AC_CHECK_SIZEOF(unsigned char, 1)
  AC_CHECK_SIZEOF(signed short, 2)
  AC_CHECK_SIZEOF(unsigned short, 2)
  AC_CHECK_SIZEOF(int, 4) dnl defines SIZEOF_INT
  AC_CHECK_SIZEOF(unsigned, 4)
  AC_CHECK_SIZEOF(long, 4)
  AC_CHECK_SIZEOF(unsigned long, 4)
  AC_CHECK_SIZEOF(long long, 4)
  AC_CHECK_SIZEOF(unsigned long long, 4)
  AC_CHECK_SIZEOF(int *, 4) dnl defines SIZEOF_INT_P
  AC_CHECK_SIZEOF(float)
  AC_CHECK_SIZEOF(double)
  AC_CHECK_SIZEOF(long double)

if test "x$enable_posix" = "x1" ; then
dnl Try to figure out which field of struct dirent contains the entry string
dnl length.
AC_MSG_CHECKING(for (struct dirent).d_namlen)
AC_TRY_LINK([
#include <sys/types.h>
#include <dirent.h>
], [
	struct dirent	ent;

	ent.d_namlen = 0;
], AC_MSG_RESULT(yes), AC_MSG_RESULT(no)

dnl d_namlen doesn't exist, so try d_reclen.
AC_MSG_CHECKING(for (struct dirent).d_reclen)
AC_TRY_LINK([
#include <sys/types.h>
#include <dirent.h>
], [
	struct dirent	ent;

	ent.d_reclen = 0;
], AC_MSG_RESULT(yes)
   AC_DEFINE(CW_LIBONYX_USE_DIRENT_RECLEN),
   AC_MSG_RESULT(no)
   AC_MSG_ERROR(struct dirent too deviant)))

  AC_CHECK_FUNCS(unsetenv)
fi
fi

dnl 
dnl onyx configuration.
dnl 
if test "x$bin_onyx" = "x1" ; then
  if test "x$prefix" = "xNONE" ; then
    CW_ONYX_RPATH="/usr/local/share/onyx/nx"
    CW_ONYX_MPATH="/usr/local/share/onyx/nxm"
  else
    CW_ONYX_RPATH="$prefix/share/onyx/nx"
    CW_ONYX_MPATH="$prefix/share/onyx/nxm"
  fi
  AC_DEFINE_UNQUOTED(CW_ONYX_RPATH, "$CW_ONYX_RPATH")
  AC_DEFINE_UNQUOTED(CW_ONYX_MPATH, "$CW_ONYX_MPATH")

  AC_DEFINE_UNQUOTED(CW_TONYX_ONYX, "$abs_objdir/bin/onyx/bin/onyx")

  tmpath=ONYX_MPATH=:.
  trpath=ONYX_RPATH=:.
  for i in $mods ; do
    tmpath=$tmpath:$abs_objdir/mod/$i/nxm
    trpath=$trpath:$abs_objdir/mod/$i/nx
  done
  AC_DEFINE_UNQUOTED(CW_TONYX_RPATH, "$trpath")
  AC_DEFINE_UNQUOTED(CW_TONYX_MPATH, "$tmpath")
fi

dnl 
dnl slate configuration.
dnl 
if test "x$bin_slate" = "x1" ; then
  if test "x$prefix" = "xNONE" ; then
    CW_SLATE_RPATH="/usr/local/share/slate/nx:/usr/local/share/onyx/nx"
    CW_SLATE_MPATH="/usr/local/share/slate/nxm:/usr/local/share/onyx/nxm"
  else
    CW_SLATE_RPATH="$prefix/share/slate/nx:$prefix/share/onyx/nx"
    CW_SLATE_MPATH="$prefix/share/slate/nxm:$prefix/share/onyx/nxm"
  fi
  AC_DEFINE_UNQUOTED(CW_SLATE_RPATH, "$CW_SLATE_RPATH")
  AC_DEFINE_UNQUOTED(CW_SLATE_MPATH, "$CW_SLATE_MPATH")

  AC_DEFINE_UNQUOTED(CW_TSLATE_SLATE, "$abs_objdir/bin/slate/bin/slate")

  tmpath=SLATE_MPATH=:.
  trpath=SLATE_RPATH=:.
  for i in $mods ; do
    tmpath=$tmpath:$abs_objdir/mod/$i/nxm
    trpath=$trpath:$abs_objdir/mod/$i/nx
  done
  AC_DEFINE_UNQUOTED(CW_TSLATE_RPATH, "$trpath")
  AC_DEFINE_UNQUOTED(CW_TSLATE_MPATH, "$tmpath")
fi

dnl 
dnl modgtk configuration.
dnl 
if test "x$mod_gtk" = "x1" ; then
  cfgoutputs="$cfgoutputs mod/modgtk/nx/modgtk/modgtk_defs.nx"
fi

dnl 
dnl Print out the results of configuration.
dnl 
AC_MSG_RESULT(===============================================================================)
AC_MSG_RESULT(CFLAGS       : ${CFLAGS})
AC_MSG_RESULT(CPPFLAGS     : ${CPPFLAGS})
AC_MSG_RESULT(LDFLAGS      : ${LDFLAGS})
AC_MSG_RESULT(LIBS         : ${LIBS})
AC_MSG_RESULT()
AC_MSG_RESULT(objdir       : ${objdir})
AC_MSG_RESULT(abs_objdir   : ${abs_objdir})
AC_MSG_RESULT(srcdir       : ${srcdir})
AC_MSG_RESULT(abs_srcdir   : ${abs_srcdir})
AC_MSG_RESULT(prefix       : ${prefix})
AC_MSG_RESULT()
AC_MSG_RESULT(libs         :${libs})
AC_MSG_RESULT(mods         :${mods})
AC_MSG_RESULT(bins         :${bins})
AC_MSG_RESULT(docs         :${docs})
AC_MSG_RESULT()
AC_MSG_RESULT(threads      : ${enable_threads})
AC_MSG_RESULT(real         : ${enable_real})
AC_MSG_RESULT(posix        : ${enable_posix})
AC_MSG_RESULT(posix-file   : ${enable_posix_file})
AC_MSG_RESULT(inlines      : ${enable_inlines})
AC_MSG_RESULT(libedit      : ${enable_libedit})
AC_MSG_RESULT(libonyx      : ${enable_libonyx})
AC_MSG_RESULT(debug        : ${enable_debug})
AC_MSG_RESULT(profile      : ${enable_profile})
AC_MSG_RESULT(docs         : ${with_docs})
AC_MSG_RESULT(===============================================================================)

dnl Create additional directories that processed files will land in.
mkdir -p $objdir/doc/latex

dnl Process .in files.
AC_OUTPUT($cfgoutputs)

dnl autoconf doesn't keep the execute bit if it's set for the input files, so do
dnl it manually.
echo chmod u+x $objdir/test/verify
chmod u+x $objdir/test/verify
